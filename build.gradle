apply plugin: 'java'
apply plugin: 'application'
apply plugin: 'gradle-one-jar'

// Use Java 7 by default
sourceCompatibility = '1.7'
targetCompatibility = '1.7'

// UTF-8 should be standard by now. So use it!
[compileJava, compileTestJava]*.options*.encoding = 'UTF-8'

// The main class of the application
mainClassName = 'com.example.helloworld.HelloWorldApplication'

buildscript {
    repositories {
        mavenCentral()
    }

    dependencies {
        classpath 'com.github.rholder:gradle-one-jar:1.0.4'
    }
}

// Set our project variables
project.ext {
    dropwizardVersion = '0.7.0'
}

repositories {
    mavenCentral()
}

dependencies {
    compile (
            'io.dropwizard:dropwizard-core:' + dropwizardVersion,
            'io.dropwizard:dropwizard-auth:' + dropwizardVersion,
            'io.dropwizard:dropwizard-assets:' + dropwizardVersion,
            'io.dropwizard:dropwizard-hibernate:' + dropwizardVersion,
            'io.dropwizard:dropwizard-migrations:' + dropwizardVersion,
            'io.dropwizard:dropwizard-views-freemarker:' + dropwizardVersion,
            'io.dropwizard:dropwizard-views-mustache:' + dropwizardVersion,
            'com.h2database:h2:1.3.175'
    )
}

// Configure the oneJar task
task oneJar(type: OneJar) {
    mainClass = mainClassName
}

// Configure the run task to start the Dropwizard service
run {
    args 'server', 'example.yml'

}

artifacts {
    archives oneJar
}

task migrate(type: JavaExec, dependsOn:[classes]) {
    description = 'Runs the Liquibase migrations.'
    main = mainClassName
    classpath = sourceSets.main.runtimeClasspath
    args 'db', 'migrate', 'example.yml'
}
